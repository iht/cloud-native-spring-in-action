plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.polarbookshop'
version = '0.0.1-SNAPSHOT'


ext {
	autoValueVersion = "1.10.4"
	googleJavaFormat = '1.15.0'
	guavaVersion = "33.0.0-jre"
}

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	compileOnly         "com.google.auto.value:auto-value-annotations:${autoValueVersion}"
	annotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"

	implementation "com.google.guava:guava:${guavaVersion}"
}

tasks.named('test') {
	useJUnitPlatform()
}

bootBuildImage {
	createdDate = "now"
}

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}

	java {
		target project.fileTree(project.rootDir) {
			include '**/*.java'
			exclude 'build/*'
		}

		// apply a specific flavor of google-java-format
		googleJavaFormat("${googleJavaFormat}").aosp().reflowLongStrings()

		// fix formatting of type annotations
		formatAnnotations()
	}
}
